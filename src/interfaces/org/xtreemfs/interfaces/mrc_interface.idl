// See doc/idl_conventions.txt before modifying this file.

#include "constants.idl"
#include "mrc_osd_types.idl"


module org
{
  module xtreemfs # 2010022215
  {
    module interfaces
    {
      struct Stat # 40
      {
        // POSIX attributes
        uint64_t dev; // A hash of the volume id
        uint64_t ino; // The file id
        uint32_t mode;
        uint32_t nlink;
        string user_id;
        string group_id;
        // No integer rdev
        uint64_t size;
        uint64_t atime_ns;
        uint64_t mtime_ns;
        uint64_t ctime_ns;
        uint32_t blksize;

        // XtreemFS-specific attributes
        uint64_t etag;
        uint32_t truncate_epoch;

        // Win32-specific attributes
        uint32_t attributes;
      };

      typedef sequence<Stat> StatSet # 41; // For returning 0 or 1 Stats on a conditional getattr

      struct DirectoryEntry # 42
      {
        string name;
        StatSet stbuf; // Can have 0 or 1 Stats on a readdir for names only
      };

      typedef sequence<DirectoryEntry> DirectoryEntrySet # 43;

      struct StatVFS # 44
      {        
        // POSIX attributes
        uint32_t bsize; // size of a block in bytes        
        uint64_t bavail; // number of free blocks in the file system        
        uint64_t blocks; // total number of blocks in file system
        string fsid; // volume id
        uint32_t namemax; // maximum filename length

        // XtreemFS-specific attributes
        AccessControlPolicyType access_control_policy;
        StripingPolicy default_striping_policy;
        uint64_t etag;
        uint32_t mode;
        string name;
        string owner_group_id;
        string owner_user_id;
      };

      typedef sequence<StatVFS> StatVFSSet # 45;


      interface MRCInterface # 200
      {
        // Constants
        const uint32_t HTTP_PORT_DEFAULT = 30636;
        const uint32_t ONCRPC_PORT_DEFAULT = 32636;
        const uint32_t ONCRPCG_PORT_DEFAULT = 32636;
        const uint32_t ONCRPCS_PORT_DEFAULT = 32636;
        const uint32_t ONCRPCU_PORT_DEFAULT = 32636;
        // Flags passed in setattr's to_set
        const uint32_t SETATTR_MODE = 1;
        const uint32_t SETATTR_UID = 2;
        const uint32_t SETATTR_GID = 4;
        const uint32_t SETATTR_SIZE = 8;
        const uint32_t SETATTR_ATIME = 16;
        const uint32_t SETATTR_MTIME = 32;
        const uint32_t SETATTR_CTIME = 64;
        const uint32_t SETATTR_ATTRIBUTES = 128;

        // POSIX/FUSE operations
        void close( VivaldiCoordinates client_vivaldi_coordinates, XCap write_xcap ) # 1;
        void fsetattr( Stat stbuf, uint32_t to_set, XCap xcap ) # 2;
        void ftruncate( XCap write_xcap, out XCap truncate_xcap ) # 3;
        void getattr( string path, uint64_t known_etag, out StatSet stbuf ) # 4;
        void getxattr( string path, string name, out string value ) # 5;
        void link( string target_path, string link_path ) # 6;
        void listxattr( string path, out StringSet names ) # 7;
        void mkdir( string path, uint32_t mode ) # 8;
        void open( string path, uint32_t flags, uint32_t mode, uint32_t attributes, VivaldiCoordinates client_vivaldi_coordinates, out FileCredentials file_credentials ) # 9;
        void readdir( string path, uint64_t known_etag, uint16_t limit_directory_entries_count, boolean names_only, uint64_t seen_directory_entries_count, out DirectoryEntrySet directory_entries ) # 10;
        void readlink( string path, out string link_target_path ) # 11;
        void removexattr( string path, string name ) # 12;
        void rename( string source_path, string target_path, out FileCredentialsSet file_credentials ) # 13;
        void rmdir( string path ) # 14;
        void setattr( string path, Stat stbuf, uint32_t to_set ) # 15;
        void setxattr( string path, string name, string value, int flags ) # 16;
        void statvfs( string volume_name, uint64_t known_etag, out StatVFSSet stbuf ) # 17;
        void symlink( string target_path, string link_path ) # 18;
        void unlink( string path, out FileCredentialsSet file_credentials ) # 19;

        // XtreemFS-specific operations
        void xtreemfs_checkpoint() # 30;
        void xtreemfs_check_file_exists( string volume_id, StringSet file_ids, string osd_uuid, out string bitmap ) # 31;
        void xtreemfs_dump_database( string dump_file ) # 32;
        void xtreemfs_get_suitable_osds( string file_id, uint32_t num_osds, out StringSet osd_uuids ) # 33;
        void xtreemfs_internal_debug( string operation, out string result ) # 34;
        void xtreemfs_lsvol( out StatVFSSet volumes ) # 35;
        void xtreemfs_mkvol( StatVFS volume ) # 36;
        void xtreemfs_renew_capability( in XCap old_xcap, out XCap renewed_xcap ) # 37;
        void xtreemfs_replication_to_master() # 38;
        void xtreemfs_replica_add( string file_id, Replica new_replica ) # 39;
        void xtreemfs_replica_list( string file_id, out ReplicaSet replicas ) # 40;
        void xtreemfs_replica_remove( string file_id, string osd_uuid, out XCap delete_xcap ) # 41;
        void xtreemfs_restore_database( string dump_file ) # 42;
        void xtreemfs_restore_file( string file_path, string file_id, uint64_t file_size, string osd_uuid, int32_t stripe_size ) # 43;
        void xtreemfs_rmvol( string volume_name ) # 44;
        void xtreemfs_shutdown() # 45;

        // Exceptions
        exception ConcurrentModificationException # 50 { string stack_trace; };
        exception errnoException # 51 { uint32_t error_code; string error_message; string stack_trace; };
        exception InvalidArgumentException # 52 { string error_message; };
        exception MRCException # 53 { uint32_t error_code; string error_message; string stack_trace; };
        exception ProtocolException # 54 { uint32_t accept_stat; uint32_t error_code; string stack_trace; };
        exception RedirectException # 55 { string address; uint16_t port; };
        exception StaleETagException # 56 { };
      };
    };
  };
};
