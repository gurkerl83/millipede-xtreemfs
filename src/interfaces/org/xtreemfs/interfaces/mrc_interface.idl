// See doc/idl_conventions.txt before modifying this file.

#include "constants.idl"
#include "mrc_osd_types.idl"


module org
{
  module xtreemfs # 2010022215
  {
    module interfaces
    {
      struct Stat # 40
      {
        uint64_t dev; // A hash of the volume id
        uint64_t ino; // The file id
        uint32_t mode;
        uint32_t nlink;
        string user_id;
        string group_id;
        // No integer rdev
        uint64_t size;
        uint64_t atime_ns;
        uint64_t mtime_ns;
        uint64_t ctime_ns;
        uint32_t blksize;

        // XtreemFS-specific attributes
        uint64_t etag;
        uint32_t truncate_epoch;

        // Win32-specific attributes
        uint32_t attributes;
      };

      typedef sequence<Stat> StatSet # 41;

      struct DirectoryEntry # 42
      {
        string name;
        Stat stbuf;
      };

      typedef sequence<DirectoryEntry> DirectoryEntrySet # 43;

      struct StatVFS # 44
      {
        //size of a block in bytes
        uint32_t bsize;
        //number of free blocks in the file system
        uint64_t bavail;
        //total number of blocks in file system
        uint64_t blocks;
        string fsid;
        uint32_t namelen;
      };

      struct Volume # 45
      {
        AccessControlPolicyType access_control_policy;
        StripingPolicy default_striping_policy;
        string id;
        uint32_t mode;
        string name;
        string owner_group_id;
        string owner_user_id;
      };

      typedef sequence<Volume> VolumeSet # 46;


      interface MRCInterface # 200
      {
        // Constants
        const uint32_t HTTP_PORT_DEFAULT = 30636;
        const uint32_t ONCRPC_PORT_DEFAULT = 32636;
        const uint32_t ONCRPCG_PORT_DEFAULT = 32636;
        const uint32_t ONCRPCS_PORT_DEFAULT = 32636;
        const uint32_t ONCRPCU_PORT_DEFAULT = 32636;
        // Flags passed in setattr's to_set
        const uint32_t SETATTR_MODE = 1;
        const uint32_t SETATTR_UID = 2;
        const uint32_t SETATTR_GID = 4;
        const uint32_t SETATTR_SIZE = 8;
        const uint32_t SETATTR_ATIME = 16;
        const uint32_t SETATTR_MTIME = 32;
        const uint32_t SETATTR_CTIME = 64;
        const uint32_t SETATTR_ATTRIBUTES = 128;

        // POSIX/FUSE operations
        void close( VivaldiCoordinates client_vivaldi_coordinates, XCap write_xcap ) # 1;
        void fsetattr( XCap xcap, Stat stbuf, uint32_t to_set ) # 2;
        void ftruncate( XCap write_xcap, out XCap truncate_xcap ) # 3;
        void getattr( string path, uint64_t known_etag, out StatSet stbuf ) # 4;
        void getxattr( string path, string name, out string value ) # 5;
        void link( string target_path, string link_path ) # 6;
        void listxattr( string path, out StringSet names ) # 7;
        void mkdir( string path, uint32_t mode ) # 8;
        void open( string path, uint32_t flags, uint32_t mode, uint32_t attributes, VivaldiCoordinates client_vivaldi_coordinates, out FileCredentials file_credentials ) # 9;
        void opendir( string path, uint64_t known_etag, uint16_t limit_directory_entries_count, out DirectoryEntrySet first_directory_entries ) # 10;
        void readdir( uint64_t known_etag, uint16_t limit_directory_entries_count, out DirectoryEntrySet more_directory_entries ) # 11;
        void readlink( string path, out string link_target_path ) # 12;
        void removexattr( string path, string name ) # 13;
        void rename( string source_path, string target_path, out FileCredentialsSet file_credentials ) # 14;
        void rmdir( string path ) # 15;
        void setattr( string path, Stat stbuf, uint32_t to_set ) # 16;
        void setxattr( string path, string name, string value, int flags ) # 17;
        void statvfs( string volume_name, out StatVFS stbuf ) # 18;
        void symlink( string target_path, string link_path ) # 19;
        void unlink( string path, out FileCredentialsSet file_credentials ) # 20;

        // XtreemFS-specific operations
        void xtreemfs_checkpoint() # 30;
        void xtreemfs_check_file_exists( string volume_id, StringSet file_ids, string osd_uuid, out string bitmap ) # 31;
        void xtreemfs_dump_database( string dump_file ) # 32;
        void xtreemfs_get_suitable_osds( string file_id, uint32_t num_osds, out StringSet osd_uuids ) # 33;
        void xtreemfs_internal_debug( string operation, out string result ) # 34;
        void xtreemfs_listdir( uint64_t known_etag, out StringSet names ) # 35; // Optimization for metadata caching
        void xtreemfs_lsvol( out VolumeSet volumes ) # 36;
        void xtreemfs_mkvol( Volume volume ) # 37;
        void xtreemfs_renew_capability( in XCap old_xcap, out XCap renewed_xcap ) # 38;
        void xtreemfs_replication_to_master() # 39;
        void xtreemfs_replica_add( string file_id, Replica new_replica ) # 40;
        void xtreemfs_replica_list( string file_id, out ReplicaSet replicas ) # 41;
        void xtreemfs_replica_remove( string file_id, string osd_uuid, out XCap delete_xcap ) # 42;
        void xtreemfs_restore_database( string dump_file ) # 43;
        void xtreemfs_restore_file( string file_path, string file_id, uint64_t file_size, string osd_uuid, int32_t stripe_size ) # 44;
        void xtreemfs_rmvol( string volume_name ) # 45;
        void xtreemfs_shutdown() # 46;

        // Exceptions
        exception ConcurrentModificationException # 50 { string stack_trace; };
        exception errnoException # 51 { uint32_t error_code; string error_message; string stack_trace; };
        exception InvalidArgumentException # 52 { string error_message; };
        exception MRCException # 53 { uint32_t error_code; string error_message; string stack_trace; };
        exception ProtocolException # 54 { uint32_t accept_stat; uint32_t error_code; string stack_trace; };
        exception RedirectException # 55 { string address; uint16_t port; };
        exception StaleETagException # 56 { };
      };
    };
  };
};
